#!/usr/bin/env bash

sed_re="/^[0-9a-f]\{16\} <[A-Za-z_][A-Za-z0-9_]\+(/,/^\$/p"
if [ -n "$2" ]; then
    sed_re="/^[0-9a-f]\{16\} <$2(/,/^\$/p"
fi

objdump -dC "$1" | sed -n "${sed_re}" | awk -F'\t' '
function min(x,y) { return x<y ? x : y }
function max(x,y) { return x>y ? x : y }
function hex2int(s) {
    gsub(/[^0-9a-f]/, "", s)
    return strtonum( "0x" s )
}
function set(s,n,c) { 
    return( substr(s,1,n-1) c substr(s,n+length(c)) ) 
}

function graph(lineArray, firstLine, numLines) {
    maxId=1

    for(nr=firstLine; nr<=firstLine+numLines; nr++) {
        split(lineArray[nr], field, "\t")
        if( field[3]~/^j/ ) {
            srcLine[maxId]=nr
            split(field[3], subfield, " ")
            dstAddr[maxId]=hex2int(subfield[2])
            maxId++
        }
        lineAddr=hex2int(field[1])
        lineToAddr[nr]=lineAddr
        addrToLine[lineAddr]=nr
    }

    # Compute arc lengths and directions in terms of line-numbers:
    for(id=1; id<maxId; id++) {
        dstLine[id] = addrToLine[dstAddr[id]]
        arcUpr[id]  = min(srcLine[id], dstLine[id])
        arcLwr[id]  = max(srcLine[id], dstLine[id])
        arcLen[id]  = arcLwr[id]-arcUpr[id]
        arcDir[id]  = srcLine[id] < dstLine[id]
        arcIdByUpr[arcUpr[id]] = id "," arcIdByUpr[arcUpr[id]]
        arcIdByLwr[arcLwr[id]] = id "," arcIdByLwr[arcLwr[id]]
    }

    # Sort by jump length:
    asorti(arcLen, idByLenRank, "@val_num_asc")

    # Starting with the shortest jumps, try to share lanes:
    laneCount=1
    for(j=1; j<maxId; j++) {
        id=idByLenRank[j]
        ul=arcUpr[id]
        ll=arcLwr[id]

        for(ln=1; ln<=laneCount; ln++) {
            split(idByLaneNum[ln], ids, ",")
            conflict=0
            for(i=1; i<=length(ids)-1; i++) {
                cul=arcUpr[ids[i]]
                cll=arcLwr[ids[i]]
                if(!(ul>cll || ll<cul)) {
                    conflict=1
                    break
                }
            }
            if(!conflict) {
                laneNumById[id] = ln
                idByLaneNum[ln] = id "," idByLaneNum[ln]
                break
            }
        }
        if(ln>laneCount) {
            laneNumById[id] = ln
            idByLaneNum[ln] = id ","
            laneCount++
        }
    }

    # Print out the asm with jump arcs:
    laneStr=sprintf("%*s", 2*laneCount+2, "") 
    for(nr=firstLine; nr<=firstLine+numLines; nr++) {
        if( nr in arcIdByUpr || nr in arcIdByLwr ) {
            if( nr in arcIdByUpr ) {
                idList=arcIdByUpr[nr]
                corner = ","
            } else {
                idList=arcIdByLwr[nr]
                corner = "`"
            }

            maxLane   = 0
            minLane   = laneCount+10
            direction = 0

            split(idList, idArray, ",")
            for(i=1; i<length(idArray); i++) {
                id = idArray[i];
                laneNum    = laneCount-laneNumById[id]+1;
                minLane    = min(laneNum, minLane)
                direction += srcLine[id] == nr
                selfLoop   = selfLoop || arcLen[id]==0
            }

            dashes=sprintf("%*s", 2*(laneCount-minLane), "")
            gsub(/ /, "-", dashes)
            laneStr=set(laneStr, 2*minLane+1, dashes)

            if(selfLoop) {
                arrow = "-o"
                corner= "o"
            } else {
                arrow = direction ? "-<" : "->"
            }
            laneStr=set(laneStr, 2*laneCount+1, arrow)

            for(i=1; i<length(idArray); i++) {
                id = idArray[i];
                laneNum = laneCount-laneNumById[id]+1;
                laneStr = set(laneStr, 2*laneNum, corner)
            }

            print laneStr line[nr]

            laneStr=sprintf("%*s", 2*laneCount+2, "") 
            for(id=1; id<=maxId; id++) {
                laneNum=laneCount-laneNumById[id]+1
                if( arcUpr[id]<=nr && nr<arcLwr[id] ) {
                    laneStr=set(laneStr, 2*laneNum, "|")
                }
            }
        } else {
            print laneStr line[nr]
        }
    }
}


BEGIN {
    IGNORECASE=1
    maxFuncs=1
}
$0=="" {
    breaks[maxFuncs]=NR
    maxFuncs++
}
{
    line[NR]=gensub(/<([a-z_][a-z0-9:_]+)\([^\)]*\)(::[a-z_][a-z0-9_]+)?(\+0x[0-9a-f]+)?>(:)?$/, "<\\1(...)\\2\\3>\\4", "g")
}
END {
    firstLine=1
    for(funcNo=1; funcNo<maxFuncs; funcNo++) {
        lastLine=breaks[funcNo]
        graph(line, firstLine, lastLine-firstLine)
        firstLine=lastLine+1
    }
}
'

